---
title: "SOPを使ってジェネリックにCase Analysis関数を実装する"
emoji: "💊"
type: "tech"
topics: ["Haskell"]
published: true
---

case analysis関数が何かについては、まず kakkun61氏 の以下の記事を参照してください。

https://kakkun61.hatenablog.com/entry/2021/01/06/Case_Analysis_%E9%96%A2%E6%95%B0

case analysis関数はデータ型毎に定まる関数ですがよくよく見てみるとシンプルなルールで統一的に実装できそうです。

https://twitter.com/lotz84_/status/1346780048653619200

Haskellで同じ名前の関数を使って複数のデータ型を扱えるようにするには、モジュールを分けたり型クラスを利用したり工夫する必要があります。

https://twitter.com/ryotakameoka/status/1346796279955767299

この記事ではジェネリックプログラミングの考え方に基づき、様々なデータ型に対応した一つのcase analysis関数を実装してみたいと思います。

これから実装する`gfold'`(generic fold)という関数は以下のような振る舞いをするようになります。

```hs
> :t unFun . gfold' @Bool
unFun . gfold' @Bool :: Bool -> r -> r -> r

> :t unFun . gfold' @(Maybe Bool)
unFun . gfold' @(Maybe Bool) :: Maybe Bool -> r -> (Bool -> r) -> r

> :t unFun . gfold' @(Either Bool Int)
unFun . gfold' @(Either Bool Int)
  :: Either Bool Int -> (Bool -> r) -> (Int -> r) -> r
```

SOP
---
SOPは `sums of products` の略でデータ型を直積の直和に分解する考え方です。[SOPの論文](https://www.andres-loeh.de/TrueSumsOfProducts/)で解説されている実装をライブラリにしたものが[`generics-sop`](https://hackage.haskell.org/package/generics-sop)です。

直和や直積といったワードに慣れない方はまずは以下の記事を参照してみてください。

https://ryota-ka.hatenablog.com/entry/2018/07/09/110000

通常、代数的データ型の文脈で直和や直積と言うと`Either`とタプル`(,)`を使って表現しますが、実際に使うデータ型では複数の型による直積と直和を考えることが多いためSOPでは n-ary sums を表す`NS`と n-ary products を表す `NP` という型を使います（SOPが sum"s" of product"s" の略であることを思い出してください）。どちらも型レベルリストによって直積あるいは直和される型を管理しています。

実際にSOPを使って得られるデータ型の表現を見てみましょう。

```hs
> from (Just 'a')
SOP (S (Z (I 'a' :* Nil)))
```

[GHC.Generics](https://hackage.haskell.org/package/base-4.14.1.0/docs/GHC-Generics.html) と違ってメタ情報が含まれていないため、単純に直和と直積の構造を扱いたい場合はSOPを使うのがシンプルでしょう。

```hs
> :t from (Just 'a')
from (Just 'a') :: SOP I '[ '[], '[Char]]
```

型を見てみるとネストされた型レベルリストがあることが分かります。外側の型レベルリストが直和に、内側の型レベルリストが直積に対応しています。 これはMaybe型の表現なので`'[]`は`Nothing`に、`'[Char]`は`Just Char`に対応しているというわけです。詳しく実装を見てみましょう。

```hs
-- | ジェネリックなSOPの表現との相互変換を扱う型クラス
class ... => Generic (a :: Type) where
    type Code a :: [[Type]]
    from :: a -> Rep a
    to :: Rep a -> a

-- | 型aのジェネリックな表現
type Rep a = SOP I (Code a)

-- | 直積の直和を表す型
newtype SOP (f :: k -> Type) (xss :: [[k]]) = SOP (NS (NP f) xss)

-- | 恒等関手
newtype I a = I a
```

`Generics`の型クラス制約に関しては気にしなくていいので省略しました。ライブラリは標準的なほとんどの型に対して`Generics`のインスタンスを定義していますし、自分で定義した型に対して`Generics`のインスタンスを自動的に導出することも可能です。

N個の型の直和を表す`NS`は以下の様に定義されています。

```hs
data NS :: (k -> Type) -> [k] -> Type where
  Z :: f x -> NS f (x ': xs)
  S :: NS f xs -> NS f (x ': xs)
```

ペアノの自然数のような実装になっていますね。これは型レベルリストは直和を表しているため値としてはリストに含まれるいずれかの型の値しか持っていないので**何番目の型の値を持っているのかを表すために自然数のような実装**になっています。例えば以下のような`Char`と`Int`と`String`の直和を表すことができます。

```hs
> Z (I 'a') :: NS I '[Char, Int, String]
Z (I 'a')
> S $ Z (I 1) :: NS I '[Char, Int, String]
S (Z (I 1))
> S . S $ Z (I "abc") :: NS I '[Char, Int, String]
S (S (Z (I "abc")))
```

次にN個の型の直積を表す`NP`の実装を見てみましょう。

```hs
data NP :: (k -> Type) -> [k] -> Type where
  Nil  :: NP f '[]
  (:*) :: f x -> NP f xs -> NP f (x ': xs)
```

これは言わずもがなへテロリストと同様の実装になっていますね。例えば以下のような`Char`と`Int`と`String`の直積を表すことができます。

```hs
> I 'a' :* I 1 :* I "abc" :* Nil :: NP I '[Char, Int, String]
I 'a' :* I 1 :* I "abc" :* Nil
```

ジェネリックなcase analysis関数の型
--------------------------------
それではSOPを使ってジェネリックなcase analysis関数`gfold`を実装していきましょう。

まず型`a`に関するcase analysis関数の型を考えてみましょう。

```hs
gfold :: (直和の各成分を処理する関数の直積) -> a -> r
```

このように書けるはずですがカッコ内の関数の数は`a`の形によって変わってくるため、このままでは表現することが難しいです。そこで`a`の表現`Rep a`が`SOP I xss`だったとしてもう一度case analysis関数の型を考えてみましょう。

```hs
gfold :: SOP I xss -> Fun xss r
```

第一引数は`a`の表現です。返り値の型である`Fun`はこれから定義しますが、`(直和の各成分を処理する関数の直積) -> r`を表していると考えてください。`Fun`は型族を扱うための型であり、方針としては`a`のSOPの構造を反映した`xss`を使って`直和の各成分を処理する関数の直積`の型を型族によって求めようと考えます。

例えば型の直積`'[a, b, c]`から`r`への関数はカリー化を考えると`a -> b -> c -> r`となるでしょう。これを型族で実装すると以下のように書けます。

```hs
type family FunP (xs :: [Type]) r where
    FunP '[] r = r
    FunP (x ': xs) r = x -> FunP xs r
```

更に型の直和`'[a, b, c]`から`r`への関数は`(a -> r, b -> r, c -> r)`と書けるでしょう。case analysis関数とし最終的に必要なのはこの対応の逆`(a -> r, b -> r, c -> r) -> '[a, b, c] -> r`であり、`'[a, b, c]`はSOPで得られているので`(a -> r, b -> r, c -> r) -> r`を得るための型族を実装します。

```hs
type family FunS (xss :: [[Type]]) r where
    FunS '[] r = r
    FunS (xs ': xss) r = FunP xs r -> FunS xss r
```

内側のリストが直積であることも考慮に入れて実装には`FunP`も利用しています。

最後に`FunS`は単射な型族ではないため型として取り扱うのが面倒です。なので`FunS`を単純に`newtype`によってラップした`Fun`を用意しておきます。

```hs
newtype Fun xss r = Fun {unFun :: FunS xss r}
```

これで `gfold` の型は完成です。次は実装に進みましょう。

ジェネリックなcase analysis関数の実装
---------------------------------
`gfold`は型レベルリストを型変数に持つため型クラスを使って帰納的に定義するのが良さそうです。

```hs
class GFold (xss :: [[Type]]) where
    gfold :: SOP I xss -> Fun xss r
```

まず`xss`が空リストの場合を考えましょう。これは型としては値を持たない`Void`に相当します。`Void`のcase analysis関数は何も値を返さない`undefined`ので実装としては以下のようにすると良さそうです。

```hs
instance GFold '[] where
    gfold _ = Fun undefined
```

次に`xss`が空リストではなく値を持つ場合を考えていきましょう。

```hs
instance GFold xss => GFold (xs ': xss) where
    gfold (SOP (S xs)) = ...
    gfold (SOP (Z x))  = ...
```

帰納的な定義を考えているので`tail`に相当する部分は`GFold`のインスタンスになっていることを前提にしています。`gfold`の実装は更に直和を表す`NS`が何かの後続`S`であるのか、`Z`であるのかによって場合分けが行われます。後続`S`である場合は帰納的に`xs`に対して再び`gfold`を行うだけです。

```hs
    gfold (SOP (S xs)) = constFun (gfold (SOP xs))
```

ここで`constFun`は型レベルリストの型を合わせるための関数です。

```hs
constFun :: Fun xss r -> Fun (xs ': xss) r
constFun (Fun f) = Fun $ const f
```

ようするに`xs`に対応する関数を無視するようにして型を合わせています。case analysisが直和で対応する関数だけ実行するという挙動がこれによって実現されるわけです。

次に直和で対応する関数を実行する実装に当たる`Z`のケースを見てみましょう。

```hs
    gfold (SOP (Z x))  = embed (Fun $ \f -> apply f x)
```

`embed`と`apply`は未定義なので後述します。直和の型に対応する値`x`が存在した場合、`Fun $ \f ->`によって対応する関数を取り出します。`f`の型は`a -> b -> c -> r`の様になっていて`x`の型は`NP I (xs :: [Type])`の様になっているので、関数適用するための工夫が必要です。そのため以下のような型クラスを用意します。

```hs
class Apply (xs :: [Type]) where
    apply :: FunP xs r -> NP I xs -> r

instance Apply '[] where
    apply r _ = r

instance Apply xs => Apply (x ': xs) where
    apply f ((I x) :* xs) = apply (f x) xs
```

これを使って`apply f x`とすることで`f`を`x`によって評価することができます。

`Fun $ \f -> apply f x`の型は結局`Fun '[xs] r`になりますが、型を合わせるためには`Fun (xs ': xss) r`にする必要があります。そこで必要になるのが`embed`です。`embed`も型クラスによって以下のように実装されています。

```hs
class Embed (xss :: [[Type]]) where
    embed :: Fun (xs ': '[]) r -> Fun (xs ': xss) r

instance Embed '[] where
    embed = id

instance Embed xss => Embed (xs ': xss) where
    embed = flipFun . constFun . embed

flipFun :: Fun (xs ': ys ': xss) r -> Fun (ys ': xs ': xss) r
flipFun f = Fun $ \ys xs -> unFun f xs ys
```

つまりcase analysis関数として対応する関数で評価した後に並んでいる関数は全て無視する`constFun`様な実装になっています。

以上により最終的な`gfold`の実装は以下のようになります。

```hs
class GFold (xss :: [[Type]]) where
    gfold :: SOP I xss -> Fun xss r

instance GFold '[] where
    gfold _ = Fun undefined

instance (Apply xs, Embed xss, GFold xss) => GFold (xs ': xss) where
    gfold (SOP (S xs)) = constFun (gfold (SOP xs))
    gfold (SOP (Z x))  = embed (Fun $ \f -> apply f x)

constFun :: Fun xss r -> Fun (xs ': xss) r
constFun (Fun f) = Fun $ const f


class Apply (xs :: [Type]) where
    apply :: FunP xs r -> NP I xs -> r

instance Apply '[] where
    apply r _ = r

instance Apply xs => Apply (x ': xs) where
    apply f ((I x) :* xs) = apply (f x) xs


class Embed (xss :: [[Type]]) where
    embed :: Fun (xs ': '[]) r -> Fun (xs ': xss) r

instance Embed '[] where
    embed = id

instance Embed xss => Embed (xs ': xss) where
    embed = flipFun . constFun . embed

flipFun :: Fun (xs ': ys ': xss) r -> Fun (ys ': xs ': xss) r
flipFun f = Fun $ \ys xs -> unFun f xs ys
```

実装した`gfold`を使ってジェネリックなcase analysis関数を実装しましょう。

```hs
gfold' :: (GFold (Code a), Generic a) => a -> Fun (Code a) r
gfold' = gfold . from
```

本当は `gfold' = unFun . gfold . from` としたいところですが、`Fun` の中身は`a`が与えられるまで決まらないのでこの実装ではコンパイルを通すことはできません。

冒頭に上げた例をもう一度見てみましょう。

```hs
> :t unFun . gfold' @Bool
unFun . gfold' @Bool :: Bool -> r -> r -> r

> :t unFun . gfold' @(Maybe Bool)
unFun . gfold' @(Maybe Bool) :: Maybe Bool -> r -> (Bool -> r) -> r

> :t unFun . gfold' @(Either Bool Int)
unFun . gfold' @(Either Bool Int)
  :: Either Bool Int -> (Bool -> r) -> (Int -> r) -> r
```

もちろん実際に実行することも可能です。

```hs
> (unFun . gfold') True 1 2
2

> (unFun . gfold') (Just 1) "empty" show
"1"

> (unFun . gfold') (Right "Haskell") (++ "??") (++ "!!")
"Haskell!!"
```

ここで実装した`gfold'`は`Generic`のインスタンスにさえしてしまえば自前で実装した型にも適用することができます。

----

ところでリストについてはどうでしょうか？

```hs
> :t unFun . gfold' @[Int]
unFun . gfold' @[Int] :: [Int] -> r -> (Int -> [Int] -> r) -> r
```

リストのcase analysis関数を思い出すと

```hs
> :t GHC.OldList.foldr
GHC.OldList.foldr :: (a -> b -> b) -> b -> [a] -> b
```

となっていて処理を行う関数側にはリストの型が現れていないことが分かります。

これを実現するためにはRecursion SchemesにおけるCatamorphismという考え方に触れる必要があります。

```hs
cata :: (Base t a -> a) -> t -> a
```

リスト`[a]`の`Base [a]`に対応する型は

```hs
data ListF = Nil | Cons a b
```

という型であり、`ListF`のcase analysis関数を考えると

```hs
(a -> b -> b) -> b -> ListF a b -> b
```

という型になることが分かります。これと`cata`を組み合わせれば

```hs
(a -> b -> b) -> b -> [a] -> b
```

となりリストのcase analysis関数と一致することが分かります。

これをジェネリックに作るためには

```hs
distFun :: (a -> Fun xss r) -> Fun xss (a -> r)
mapFun :: (a -> b) -> Fun xss a -> Fun xss b
```

のような関数を実装した上で

```hs
gcata = mapFun cata (distFun gfold)
```

と実装すれば良さそうです。

ただ実装は大変そうなので今回は方針に触れるだけで終わりにしたいと思います。

----

＼読んでいただきありがとうございました！／
この記事が面白かったら いいね♡ をいただけると嬉しいです☺️
100円からでも サポート¥ をいただければ次の記事を書くため励みになります🙌